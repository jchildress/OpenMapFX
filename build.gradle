allprojects {
    apply plugin: 'java'
    apply plugin: 'retrolambda'
    repositories {
        mavenCentral()
        mavenLocal()
    }
}
apply plugin: 'application'
apply plugin: 'signing'
apply plugin: 'maven-publish'

sourceCompatibility = '1.8'
mainClassName = "org.lodgon.openmapfx.desktop.MapView"
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.lodgon.openmapfx.desktop.MapView'
}

buildscript {
  repositories {
      mavenCentral()
  }

  dependencies {
     classpath 'me.tatarka:gradle-retrolambda:1.3.0'
  }
}

repositories {
    mavenCentral()
}

dependencies {
       testCompile group: 'junit', name: 'junit', version: '4.10'
}

task coreJar(type:Jar) {
}

artifacts {
    archives coreJar
}

task signJars(type: Sign, dependsOn: jar) {
    sign configurations.archives
}

task upload << {
    signJars
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier "source"
}

publishing {
    publications {
        core(MavenPublication) {
            groupId 'org.lodgon.openmapfx'
            artifactId 'openmapfx-core'
            version '1.0'
            pom.withXml {
                asNode().appendNode('name', 'OpenMapFX Core')
                asNode().appendNode('description', 'The OpenMapFX Core functionality')
                asNode().appendNode('url', 'http://bitbucket.org/lodgon/openmapfx')
                def license = asNode().appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The 3-Clause BSD License')
                license.appendNode('url', 'http://opensource.org/licenses/BSD-3-Clause')
                license.appendNode('distribution', 'repo')
                def developer = asNode().appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'johanvos')
                developer.appendNode('name', 'Johan Vos')
                def scm = asNode().appendNode('scm')
                scm.appendNode('connection', 'scm:hg:https://bitbucket.org/lodgon/openmapfx')
                scm.appendNode('developerConnection', 'scm:hg:https://bitbucket.org/lodgon/openmapfx')
                scm.appendNode('url', 'https://bitbucket.org/lodgon/openmapfx/overview')
            }
            artifact sourceJar
            artifact jar {
                classifier "jar"
            }
        }
    }
    repositories {
        maven {
            url= "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }
}

/******************************************************************************
 * Project specific configurations                                            *
 *****************************************************************************/

project (':leapmotion') {
    compileJava.onlyIf{project.hasProperty('LEAPSDK')}
    if (project.hasProperty('LEAPSDK')) {
        dependencies {
            compile files("$LEAPSDK/lib/LeapJava.jar");
            compile project(":");
        }
    }
    apply plugin: 'application'
    run.onlyIf{project.hasProperty('LEAPSDK')}

    if (project.hasProperty('LEAPSDK')) {
        run {
            main = "org.lodgon.openmapfx.leapmotion.LeapMotionMapView"
            jvmArgs = [ "-Djava.library.path=$LEAPSDK/lib/x64"]
        }
    }
}

project (':miataru') {
    dependencies {
            compile group:'org.codehaus.jackson', name:'jackson-mapper-asl', version:'1.9.9'
            // compile group:'org.javafxdata', name:'datasources', version:'8.0.5-SNAPSHOT'
            compile group:'org.javafxdata', name:'datafx-core', version:'2.0.2'
            compile project(":");
    }
}

project (':desktop') {
    dependencies {
        compile project(":");
        compile project(":miataru");
    }
    compileJava {
        doLast {
println ('I will copy resources');
            copy {
                from '../miataru/src/main/resources'
                into '../desktop/build/resources/main'
            }
        }
    }
    apply plugin: 'application'
    mainClassName = "org.lodgon.openmapfx.desktop.MapView"
    run {
        systemProperties System.getProperties()
    }
}

project (':android') {
    apply plugin: 'application'
    mainClassName = "org.lodgon.openmapfx.android.AndroidMapView"
    def sdkloc = ""
    def javafxsdkloc = ""
    repositories {
      mavenCentral()
    }
    compileJava.onlyIf{project.hasProperty('ANDROIDSDK')}
    if (project.hasProperty('ANDROIDSDK')) {
        sdkloc="$ANDROIDSDK"
        javafxsdkloc="$JAVAFXSDK"
        dependencies {
            compile files("$ANDROIDSDK/platforms/android-19/android.jar");
            compile files("$JAVAFXSDK/rt/lib/ext/jfxdvk.jar");
            compile project(":");
            compile project(":miataru");
        }
    }
    classes {
        doLast {
            copy {
                from ("../build/classes/main")
                into ("build/retrolambda/main")
            }
            copy {
                from ("../miataru/build/classes/main")
                into ("build/retrolambda/main")
            }
            println 'classes copied';
        }
    }

    task copydeps(type: Sync) {
        from configurations.runtime
        into "$buildDir/libs"
    }
    jar {
        archiveName 'openmapfx-android.jar'
        doLast {
            copy { 
                from configurations.runtime
                into "$buildDir/libs"
                exclude 'android.jar','jfxdvk.jar'
            }
        }
    }
    clean {
        doLast {
           File jfxdvk = file('native/libs/jfxdvk.jar');
           jfxdvk.delete();
           FileTree armeabidir = fileTree (dir: 'native/libs/armeabi');
           armeabidir.each{File f -> delete(f)}
        }
    }
    task preparenative(dependsOn: jar)  {
// apparently, configuration might go wrong even if task is not executed?
        if (project.hasProperty('ANDROIDSDK')) {
            println 'creating native directories'
            def localprop = new File (project.projectDir.getAbsolutePath()+'/native/local.properties');
            def text = 'sdk.dir='+sdkloc+'\n'
            localprop.text = text;
            def antprop = new File(project.projectDir.getAbsolutePath()+'/native/ant.properties');
            def aptext = 'jfx.sdk.absolute.dir='+javafxsdkloc+'\n';
            aptext = aptext + 'jfx.app.absolute.dist.dir='+project.projectDir+'/build/libs\n';
            aptext = aptext + 'isDalvik=true\n';
            antprop.text = aptext;
            copy {
                from javafxsdkloc+'/rt/lib/armeabi'
                into 'native/libs/armeabi'
            }
            copy {
                from javafxsdkloc+'/rt/lib/ext/jfxdvk.jar'
                into 'native/libs'
            }
            File srcdir = new File('android/native/src');
            srcdir.mkdir();
        }
    }
    preparenative.onlyIf{project.hasProperty('ANDROIDSDK')}
}
