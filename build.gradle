// maven upload taken from the ControlsFX gradle build files
// see http://controlsfx.org
allprojects {
    apply plugin: 'java'
    apply plugin: 'retrolambda'
    repositories {
        mavenCentral()
        mavenLocal()
    }
}
apply plugin: 'application'
// apply plugin: 'signing'
// apply plugin: 'maven-publish'
apply plugin: 'maven'
apply from: 'mavenPublish.gradle'

group = 'org.lodgon.openmapfx'
version = '1.0.0'

sourceCompatibility = '1.8'
mainClassName = "org.lodgon.openmapfx.desktop.MapView"
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.lodgon.openmapfx.desktop.MapView'
}

buildscript {
  repositories {
      jcenter()
  }

  dependencies {
     classpath 'me.tatarka:gradle-retrolambda:1.3.0'
     classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
     classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
  }
}

repositories {
    mavenCentral()
}

dependencies {
       testCompile group: 'junit', name: 'junit', version: '4.10'
}

javadoc {
    exclude 'impl/*'
    failOnError = true
    classpath = project.sourceSets.main.runtimeClasspath + configurations.jdk

    options.windowTitle("ControlsFX Project ${version}")
    options.links("http://docs.oracle.com/javase/8/docs/api/");
    options.links("http://docs.oracle.com/javase/8/javafx/api/");
    options.addBooleanOption("Xdoclint:none").setValue(true);
    options.overview("src/main/docs/overview.html");

    doLast {
        copy {
            from "src/main/docs"
            into "$buildDir/docs/javadoc"
        }
    }
}


task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourceJar
    archives javadocJar
}

allprojects {
    apply from: rootProject.file('gradle/code-quality.gradle')
    apply plugin: 'com.github.ben-manes.versions'
}


/******************************************************************************
 * Project specific configurations                                            *
 *****************************************************************************/


project (':leapmotion') {
    compileJava.onlyIf{project.hasProperty('LEAPSDK')}
    if (project.hasProperty('LEAPSDK')) {
        dependencies {
            compile files("$LEAPSDK/lib/LeapJava.jar");
            compile project(":");
        }
    }
    apply plugin: 'application'
    run.onlyIf{project.hasProperty('LEAPSDK')}

    if (project.hasProperty('LEAPSDK')) {
        run {
            main = "org.lodgon.openmapfx.leapmotion.LeapMotionMapView"
            jvmArgs = [ "-Djava.library.path=$LEAPSDK/lib/x64"]
        }
    }
}

project (':miataru') {
    dependencies {
            compile group:'org.codehaus.jackson', name:'jackson-mapper-asl', version:'1.9.9'
            // compile group:'org.javafxdata', name:'datasources', version:'8.0.5-SNAPSHOT'
            compile group:'org.javafxdata', name:'datafx-core', version:'2.0.2'
            compile project(":");
    }
}

project (':desktop') {
    dependencies {
        compile project(":");
        compile project(":miataru");
    }
    compileJava {
        doLast {
println ('I will copy resources');
            copy {
                from '../miataru/src/main/resources'
                into '../desktop/build/resources/main'
            }
        }
    }
    apply plugin: 'application'
    mainClassName = "org.lodgon.openmapfx.desktop.MapView"
    run {
        systemProperties System.getProperties()
    }
}

project (':android') {
    apply plugin: 'application'
    mainClassName = "org.lodgon.openmapfx.android.AndroidMapView"
    def sdkloc = ""
    def javafxsdkloc = ""
    repositories {
      mavenCentral()
    }
    compileJava.onlyIf{project.hasProperty('ANDROIDSDK')}
    if (project.hasProperty('ANDROIDSDK')) {
        sdkloc="$ANDROIDSDK"
        javafxsdkloc="$JAVAFXSDK"
        dependencies {
            compile files("$ANDROIDSDK/platforms/android-19/android.jar");
            compile files("$JAVAFXSDK/rt/lib/ext/jfxdvk.jar");
            compile project(":");
            compile project(":miataru");
        }
    }
    classes {
        doLast {
            copy {
                from ("../build/classes/main")
                into ("build/retrolambda/main")
            }
            copy {
                from ("../miataru/build/classes/main")
                into ("build/retrolambda/main")
            }
            println 'classes copied';
        }
    }

    task copydeps(type: Sync) {
        from configurations.runtime
        into "$buildDir/libs"
    }
    jar {
        archiveName 'openmapfx-android.jar'
        doLast {
            copy { 
                from configurations.runtime
                into "$buildDir/libs"
                exclude 'android.jar','jfxdvk.jar'
            }
        }
    }
    clean {
        doLast {
           File jfxdvk = file('native/libs/jfxdvk.jar');
           jfxdvk.delete();
           FileTree armeabidir = fileTree (dir: 'native/libs/armeabi');
           armeabidir.each{File f -> delete(f)}
        }
    }
    task preparenative(dependsOn: jar)  {
// apparently, configuration might go wrong even if task is not executed?
        if (project.hasProperty('ANDROIDSDK')) {
            println 'creating native directories'
            def localprop = new File (project.projectDir.getAbsolutePath()+'/native/local.properties');
            def text = 'sdk.dir='+sdkloc+'\n'
            localprop.text = text;
            def antprop = new File(project.projectDir.getAbsolutePath()+'/native/ant.properties');
            def aptext = 'jfx.sdk.absolute.dir='+javafxsdkloc+'\n';
            aptext = aptext + 'jfx.app.absolute.dist.dir='+project.projectDir+'/build/libs\n';
            aptext = aptext + 'isDalvik=true\n';
            antprop.text = aptext;
            copy {
                from javafxsdkloc+'/rt/lib/armeabi'
                into 'native/libs/armeabi'
            }
            copy {
                from javafxsdkloc+'/rt/lib/ext/jfxdvk.jar'
                into 'native/libs'
            }
            File srcdir = new File('android/native/src');
            srcdir.mkdir();
        }
    }
    preparenative.onlyIf{project.hasProperty('ANDROIDSDK')}
}
